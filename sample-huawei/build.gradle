plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.huawei.agconnect'
}

def formattedDate = new Date().format('yyyyMMddHHmm')
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
def jksDir = keystorePropertiesFile.absolutePath.substring(0,keystorePropertiesFile.absolutePath.length() - "keystore.properties".length())

android {
    namespace 'com.melody.petal.myapplication'
    compileSdk rootProject.ext.libraryInfo.compileSdk

    defaultConfig {
        minSdk rootProject.ext.libraryInfo.huaweiMinSdk
        targetSdk rootProject.ext.libraryInfo.targetSdk
        versionCode 100
        versionName "1.0.0"

        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        debug {
            storeFile file(jksDir + keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
        release {
            storeFile file(jksDir + keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    buildFeatures {
        compose true
    }
    composeOptions  {
        kotlinCompilerExtensionVersion "1.3.2" 
        kotlinCompilerVersion "1.7.10"
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "huawei_${defaultConfig.versionName}_${formattedDate}-${variant.buildType.name}.apk"
        }
    }
}

dependencies {
    implementation project(path: ':sample-common')
    implementation project(path: ':sample-ui-components')
    // https://developer.huawei.com/consumer/cn/doc/development/HMSCore-Guides/android-sdk-integrating-sdk-0000001061671869#section52581942164415
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.huawei.agconnect:agconnect-core:1.9.1.300'
    implementation project(path: ':huawei-map-compose')
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.12'
}