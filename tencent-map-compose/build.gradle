plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-parcelize'
    id 'maven-publish'
}

android {
    compileSdk 33

    defaultConfig {
        minSdk 21
        targetSdk 33

        consumerProguardFiles "consumer-rules.pro"
    }
    buildFeatures {
        compose true

        // Disable unused AGP features
        buildConfig false
        aidl false
        renderScript false
        shaders false
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    publishing {
        singleVariant("release") {
            // if you don't want sources/javadoc, remove these lines
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.1'

    // 地图库
    api 'com.tencent.map:tencent-map-vector-sdk:4.5.12'
    // 地图组件库，包括小车平移、点聚合等组件功能。
    api 'com.tencent.map:sdk-utilities:1.0.8'
    // 定位SDK
    api 'com.tencent.map.geolocation:TencentLocationSdk-openplatform:7.5.0'
}

// 生成的aar等产物： ./gradlew clean build publish
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId 'io.github.TheMelody'
                artifactId 'tencent_compose'
                version = '1.0.0'
            }
        }
        repositories {
            maven {
                url "$buildDir/repo"
            }
        }
    }
}