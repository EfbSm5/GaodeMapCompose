plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    alias(libs.plugins.compose.compiler)
}

def formattedDate = new Date().format('yyyyMMddHHmm')
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
def jksDir = keystorePropertiesFile.absolutePath.substring(0,keystorePropertiesFile.absolutePath.length() - "keystore.properties".length())

android {
    namespace 'com.melody.map.myapplication'
    compileSdk rootProject.ext.libraryInfo.compileSdk

    defaultConfig {
        applicationId "com.melody.map.myapplication"
        minSdk rootProject.ext.libraryInfo.minSdk
        targetSdk rootProject.ext.libraryInfo.targetSdk
        versionCode 105
        versionName "1.0.5"

        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        debug {
            storeFile file(jksDir + keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
        release {
            storeFile file(jksDir + keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_19
        targetCompatibility JavaVersion.VERSION_19
    }
    buildFeatures {
        compose true
    }
    kotlinOptions {
        jvmTarget = '19'
    }
    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.14"
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    lint {
        checkReleaseBuilds false
        abortOnError false
    }
    android.applicationVariants.configureEach { variant ->
        variant.outputs.configureEach {
            outputFileName = "gaode_${defaultConfig.versionName}_${formattedDate}-${variant.buildType.name}.apk"
        }
    }
}

dependencies {
    implementation project(path: ':sample-common')
    implementation project(path: ':sample-ui-components')
    implementation project(path: ':gd-map-compose')
    debugImplementation libs.leakcanary.android
}